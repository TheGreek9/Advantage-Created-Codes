Option Explicit
Sub IRI()
'If seeking help with macro, contact: Spyro Ziangos - spyro.ziangos@advantagesolutions.net
Dim tabCount As Long
tabCount = ThisWorkbook.Worksheets.Count
If (tabCount = 1) Then
    MsgBox "More Than One Tab Required To Work", vbExclamation
    End
End If
ReDim geo(0 To tabCount - 2) As Variant

Dim colHead() As Variant
Dim colLast As Long
Dim i As Long
Dim name As String
Dim rowLast As Long
Dim s1ColLast As Long
Dim s1RowLast As Long
Dim sheetLast As Long
Dim shp As Shape
Dim cell As Variant
Application.ScreenUpdating = False

On Error GoTo ErrorHandler

With ThisWorkbook
    
    If (.Worksheets(2).Range("A1").Value = "Geographies") Then
        MsgBox "Condensed Information Already, Please Check Tab 1", vbExclamation
        End
    End If
    
    'Fill array with geographies from Index
    For i = 0 To tabCount - 2
        geo(i) = .Worksheets("Index").Cells(i + 3, 4).Value
    Next i
    
    'Delete empty rows above cells and insert first column
    For i = 2 To tabCount
        With .Worksheets(i)
            rowLast = .Range("B" & .Rows.Count).End(xlUp).Row
            If rowLast <> 1 Then
                For Each shp In .Shapes
                    shp.Delete
                Next
    
                Do While (IsEmpty(.Cells(1, 1)))
                    .Rows(1).Delete
                Loop
            
                rowLast = .Range("B" & .Rows.Count).End(xlUp).Row
            
                If (IsEmpty(.Range("A1")) = False) Then
                    .Range("A:A").Columns.Insert
                End If
            
                .Range(.Cells(2, 1), .Cells(rowLast, 1)).Value = geo(i - 2)
            
                For Each cell In .Range("B1:B" & rowLast)
                    If cell.MergeCells = True Then
                        cell.UnMerge
                    End If
                Next cell
            
                For Each cell In .Range("B1:B" & rowLast)
                    If IsEmpty(cell) = False Then
                        name = cell.Value
                    ElseIf IsEmpty(cell) Then
                        cell.Value = name
                    End If
                Next cell
                
                If i > 2 And IsEmpty(.Range("B2")) = False Then
                s1RowLast = ThisWorkbook.Worksheets(2).Range("C" & Worksheets(2).Rows.Count).End(xlUp).Row
                rowLast = .Range("B" & .Rows.Count).End(xlUp).Row
                colLast = .Cells(1, Columns.Count).End(xlToLeft).Column
                .Range(.Cells(1, 1), .Cells(rowLast, colLast)).Copy
                ThisWorkbook.Worksheets(2).Range("A" & s1RowLast + 1).PasteSpecial xlPasteValues
                Application.CutCopyMode = False
                End If
            End If
        End With
    Next
    
    With .Worksheets(2)
        s1RowLast = .Range("C" & Worksheets(2).Rows.Count).End(xlUp).Row
    
        For i = s1RowLast To 2 Step -1
            If .Cells(i, 2).Value = "Time" Then
                .Rows(i).Delete
            End If
        Next
    
        .Range("A1").Value = "Geographies"
        .Activate
        MsgBox "Success!"
    End With
End With
Application.ScreenUpdating = True
Exit Sub

ErrorHandler:
    MsgBox "Error " & Err.Number & ": " & Err.Description & vbNewLine & vbNewLine & _
    "Please Close Report And Try Again;" & vbNewLine & vbNewLine & "If Error Persists, Contact Spyro Ziangos - spyro.ziangos@advantagesolutions.net", vbExclamation
End Sub





